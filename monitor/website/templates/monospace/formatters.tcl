# formatters.tcl --
#
# formatters run within the template namespace and provide template
# specific formatting of data

package require ngis::hrformat
package require ngis::trimmers
package require textutil

# the following inclusion of snigreport
# is supposed to override the whole inclusion
# of package 'report' done in ngis::hrformat
# (by reuiring ngis::common)

package require snigreport 0.6
package require struct::snigmatrix 2.3

variable hr_formatter
variable snig_nav_bar

variable message_box_mat    [namespace current]::msgbox_mat
variable message_box_rep    [namespace current]::msgbox_rep

catch { $message_box_mat destroy }

::struct::matrix $message_box_mat

set hr_formatter [::ngis::HRFormat create [::ngis::Formatters new_cmd hr]]
::oo::objdefine $hr_formatter {mixin ::ngis::HTMLTrimmer}

set snig_nav_bar [::report::report ::snig_nav_bar 4 style simpletable]
snig_nav_bar pad 0 both "  "
snig_nav_bar pad 1 both "  "
snig_nav_bar pad 2 both "  "
snig_nav_bar pad 3 both "  "

set snig_nav_matrix [::struct::matrix ::snig_nav_matrix]

# template width in characters

proc template_width {} { return 132 }

proc entities_table {rows_l} {
    variable hr_formatter

    return [::rivet::xml [$hr_formatter c108 $rows_l] pre]
}

proc entity_service_recs {rows_l entity_description} {
    variable hr_formatter

    return [::rivet::xml [$hr_formatter c122 $rows_l $entity_description] pre]
}

###################################
#
# utility functions to manipulate 
#

proc report_width {rep_t} {
    return [string length [lindex [split $rep_t "\n"] 0]]
}

proc left_padding {rep_t padlen} {
    set pad_s [string repeat " " $padlen]
    set rep_l [lmap l [split $rep_t "\n"] {
        concat "${pad_s}$l"
    }]
    return [join $rep_l "\n"]
}

proc center_report {rep_t} {
    set rep_width [report_width $rep_t]
    set tem_width [template_width]
    if {$rep_width >= $tem_width} {
        return $rep_t
    }
    set rep_t [left_padding $rep_t [expr int(($tem_width - $rep_width) / 2)]]
    return $rep_t
}

# service_table --
#
# print a table of a service data. Argument is a dictionary
# of service data as generated by procedure ::ngis::service::service_data
#

proc service_info {service_d} {
    variable hr_formatter
    return [center_report [$hr_formatter c116single $service_d]]
}

proc service_tasks {service_d} {
    variable hr_formatter
    return [center_report [$hr_formatter c118 $service_d $::ngis::registered_tasks]]
}

proc service_table {service_d} {
    variable hr_formatter

    set service_t [::rivet::xml [service_info  $service_d] [list pre id "service_info"]]
    set task_t    [::rivet::xml [service_tasks $service_d] [list pre id "task_results"]]
    return "${service_t}\n${task_t}"
}

proc navigation_bar {rowcount urls} {
    variable snig_nav_bar
    variable snig_nav_matrix 

    set links_l [lmap symb [list \u00ab \u2039 \u203A \u00bb] u $urls {
        if {$u == ""} {
            set symb
        } else {
            ::rivet::xml $symb [list a href $u]
        }
    }]

    $snig_nav_matrix deserialize [list 1 4 [list $links_l]]
    return [::rivet::xml [$snig_nav_bar printmatrix $snig_nav_matrix] [list div id "navbar"] pre]
}

proc display_report {report_n data_ld} {
    variable hr_formatter

    switch $report_n {
        112 {
            set connections_l {}
            foreach conn_d $data_ld {
                dict with conn_d {
                    lappend connections_l [list $login $type $ncmds $protocol $idle]
                }
            }

            return [$hr_formatter c112 $connections_l]
        }
        114 {
            set jobs_l {}
            foreach job_d $data_ld {
                dict with job_d {
                    lappend jobs_l [list $gid $description $type $version $status $timestamp]
                }
            }

            return [$hr_formatter c114 $jobs_l]
        }
    }

}

proc generate_banner {language menu} {

    set links [$menu links]

    catch {::snig_banner_mat}
    catch {::snig_banner}

    set snig_banner_mat [::struct::matrix ::snig_banner_mat]
    set snig_banner [::report::report ::snig_banner [llength $links] style simpletable]
    for {set p 0} { $p < [llength $links]} {incr p} { $snig_banner pad $p both "  " }
    set links_l [lmap l $links {
        set owner   [$l link_owner]
        set href    [[$owner to_url $l] attribute href]
        set text    [$l link_text $language]
        set ltext   [::rivet::xml $text [list a href $href]]
    }]
    $snig_banner_mat deserialize [list 1 [llength $links_l] [list $links_l]]
    set snig_banner_txt [::rivet::xml [$snig_banner printmatrix $snig_banner_mat] pre]

    $snig_banner destroy
    $snig_banner_mat destroy
    return $snig_banner_txt
}

proc message_box {message_l} {
    variable message_box_mat
    variable message_box_rep

    catch { $message_box_rep destroy }

    ::report::report $message_box_rep 2 style simpletable
    for {set l 0} {$l < [llength $message_l]} {incr l} { $message_box_rep pad $l both " " }

    $message_box_mat deserialize [list [llength $message_l] 2 $message_l]
    return [::rivet::xml [$message_box_rep printmatrix $message_box_mat] pre]
}

proc report_page {} {
    return [join [list [::rivet::xml "" div [list pre id report]] [::rivet::xml "" div [list pre id response]]] "\n"]
}

