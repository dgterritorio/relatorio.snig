# -- task.tcl
#
#

package require ngis::conf
package require ngis::msglogger
package require ngis::taskmessages

namespace eval ::ngis::tasks {
    variable base_tasks [list congruence      connectivity capabilities]
    variable procedures [list data_congruence http_status  get_url     ]
    variable functions  [list ""              ""           ""          ]
    variable descriptions [list "Testing resource record data completeness and congruence" \
                                "Analyze service HTTP/HTTPS response" \
                                "Test service by checking response to HTTP requests (using Tcl's http package)" \
                                "Curl based determination of capabilities"]
    variable tasks      [list]
    variable tasks_db   [dict create]

    # throughout the monitor code we assume that every task description
    # dictionary is generated by this procedure. This namespace will be endowed with
    # the methods (procedures) needed to properly handle such dictionaries.

    # We don't want to create full-fledged oo objects because thread workers
    # are designed to handle tasks and such objects couldn't be shared among
    # threads lest the classes are (uselessly?) endowed with serialization/
    # deserialization methods

    proc mktask {t job_o {t_args ""}} {
        variable tasks_db
        variable tasks

        if {[dict exists $tasks_db $t]} {
            set job_d [$job_o serialize]
            dict unset job_d tasks

            set task_d [dict get $tasks_db $t]

            dict set task_d task   $t
            dict set task_d status ""
            dict set task_d data   ""
            dict set task_d args   ""
            dict set task_d job    $job_d
            return $task_d
        }
        return -code 1 -errorcode invalid_task
    }

    proc get_registered_tasks {} {
        variable tasks
        return $tasks
    }

    proc get_task {task} {
        variable task_db
        if {[dict exists $task_db $task]} {
            return [dict get $task_db $task]
        }
        return -code error -errorcode unregistered_task "Unregistered task '$task'"
    }

    # -- Data access methods
    #
    # we don't check the consistence of the dictionary. If a key is
    # missing there's a problem in the caller's code and it rightfully
    # must generated an error

    proc job_name {task_d} { return [dict get $task_d job jobname] }
    proc url {task_d} { return [dict get $task_d job uri] }
    proc gid {task_d} { return [dict get $task_d job gid] }
    proc procedure {task_d} { return [dict get $task_d procedure] }
    proc function {task_d} { return [dict get $task_d function] }

    proc build_tasks_database {path_list} {
        variable tasks
        variable tasks_db
        variable base_tasks
        variable procedures
        variable functions
        variable descriptions

        set tasks $base_tasks
        foreach bt $base_tasks btp $procedures btf $functions btd $descriptions {
            dict set tasks_db $bt [dict create  function    $btf \
                                                procedure   $btp \
                                                description $btd]
        }

        foreach p $path_list {
            set glist [glob [file join $p "*.sh"]]

            foreach shscript $glist {
                if {[catch {
                    set identity [exec /bin/bash $shscript identify]
                } e einfo]} {
                    ::ngis::logger emit "error analyzing shell script $shscript: $e $einfo"
                    continue
                }

                lassign $identity task description

                #### testbed bash procedure: we don't include it ####

                if {$task == "testbed"} { continue }

                # this is not a useless redundancy: through this list we
                # attempt to establish a priority sequence

                lappend tasks $task
                dict set tasks_db $task [dict create function    $shscript \
                                                     description $description \
                                                     procedure   run_bash]
            }
        }
    }

    namespace export   *
    namespace ensemble create
}

package provide ngis::task 0.3
