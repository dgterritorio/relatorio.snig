# -- task.tcl
#
#

package require ngis::conf
package require ngis::msglogger
package require ngis::taskmessages

namespace eval ::ngis::tasks {
    variable verbose true

    #variable base_tasks [list congruence     ]
    #variable procedures [list data_congruence]
    #variable functions  [list ""             ]
    #variable descriptions [list "Data congruence"]
                                #"Analyze service HTTP/HTTPS response" \
                                #"Test service by checking response to HTTP requests (using Tcl's http package)" \
                                #"Curl based determination of capabilities"
    variable tasks      [list]
    variable tasks_db   [dict create]

    # throughout the whole code we assume that every task description
    # dictionary is generated by this procedure. This namespace will be endowed with
    # the methods (procedures) needed to properly handle such dictionaries.

    # We don't want to create full-fledged oo objects because thread workers
    # are designed to handle tasks and such objects couldn't be shared among
    # threads lest the classes are (uselessly?) endowed with serialization/
    # deserialization methods

    proc mktask {t job_o {t_args ""}} {
        variable tasks_db
        variable tasks

        if {[dict exists $tasks_db $t]} {
            set job_d [$job_o serialize]
            dict unset job_d tasks

            set task_d [dict get $tasks_db $t]

            dict set task_d task   $t
            dict set task_d status ""
            dict set task_d data   ""
            dict set task_d args   ""
            dict set task_d job    $job_d
            return $task_d
        }
        return -code 1 -errorcode invalid_task
    }

    proc get_registered_tasks {} {
        variable tasks
        return $tasks
    }

    proc get_task {task} {
        variable task_db
        if {[dict exists $task_db $task]} {
            return [dict get $task_db $task]
        }
        return -code error -errorcode unregistered_task "Unregistered task '$task'"
    }

    # -- Data access methods
    #
    # we don't check the consistence of the dictionary. If a key is
    # missing there's a problem in the caller's code and it rightfully
    # must generated an error

    proc job_name {task_d} { return [dict get $task_d job jobname] }
    proc url {task_d} { return [dict get $task_d job uri] }
    proc gid {task_d} { return [dict get $task_d job gid] }
    proc uuid {task_d} { return [dict get $task_d job uuid] }
    proc type {task_d} { return [dict get $task_d job uri_type] }
    proc version {task_d} {
        if {[dict exists $task_d job version]} {
            return [dict get $task_d job version]
        } else {
            return "<unspecified>"
        }
    }
    proc procedure {task_d} { return [dict get $task_d procedure] }
    proc function {task_d} { return [dict get $task_d function] }
    proc script {task_d} { return [dict get $task_d script] }
    proc language {task_d} { return [dict get $task_d language] }

    proc term_output {str} {
        variable verbose 

        if {[string is true $verbose]} {
            puts $str
        }
    }

    proc build_tasks_database {task_path_dir args} {
        variable tasks
        variable tasks_db
        variable verbose

        set verbose true
        foreach a $args {
            if {$a == "-verbose"} {
                set verbose true
            } elseif {$a == "-quiet"} {
                set verbose false
            } else {
                puts "unrecognized option '$a'"
            }
        }

        # save current_dir to be restored
        set glist [glob [file join $task_path_dir "*"]]
        foreach script [lsort $glist] {
            term_output "analyze script: $script"
            if {[catch {
                set ftype [file extension $script]
                if {$ftype == ".sh"} {
                    set identity [exec /bin/bash $script identify]
                    lassign $identity task description
                    lappend tasks $task
                    dict set tasks_db $task [dict create function    $script \
                                                         script      $script \
                                                         description $description \
                                                         procedure   run_bash \
                                                         language    "Bash"]
                } elseif {$ftype == ".tcl"} {

                    #puts "ns: [namespace current]"
                    namespace eval [namespace current] [list source $script ]
                    set identity [eval [namespace current]::identify]
                    lassign $identity task function description
                    lappend tasks $task
                    dict set tasks_db $task [dict create function    $function \
                                                         script      $script \
                                                         description $description \
                                                         procedure   run_tcl \
                                                         language    "Tcl"]
                    rename [namespace current]::${function} ""
                }
            } e einfo]} {
                ::ngis::logger emit "error analyzing shell script $script: $e $einfo"
                continue
            }
        }
        catch {rename [namespace current]::identify ""}
    }

    proc list_registered_tasks {} {
        variable tasks
        variable tasks_db

        set tasks_l [lmap t $tasks { 
            set td [dict get $tasks_db $t]
            dict with td {
                set tlist [list $t $procedure $description [file tail $script] $language] 
            }
            set tlist
        }]
        return $tasks_l
    }

    namespace export   *
    namespace ensemble create
}

package provide ngis::task 0.3
